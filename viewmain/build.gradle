if(isModule.toBoolean()){
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

def app = rootProject.ext.android
def appcompat = rootProject.ext.appcompat
def library = rootProject.ext.library

android {

    compileSdkVersion app.compileSdkVersion
    defaultConfig {
        minSdkVersion app.minSdkVersion
        targetSdkVersion app.targetSdkVersion
        versionCode app.versionCode
        versionName app.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude '**/debug/**'
                }
            }
        }
    }

}

repositories {
    flatDir {
        dirs 'libs','../dodata/libs'
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation project(':viewmodel')
    implementation project(':baseapp')
    annotationProcessor library.arouter_compiler
    implementation library.butterknife
    annotationProcessor library.butterknife_compiler
    implementation appcompat.bottom_navigation_bar
}